import dependencies.Dep
import dependencies.Packages
import dependencies.Versions

plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
}

apply from: 'codeQuality.gradle'

def isCi = "true" == System.getenv("CI")

android {
    compileSdkVersion Versions.compileSdk
    defaultConfig {
        applicationId Packages.production
        minSdkVersion Versions.minSdk
        targetSdkVersion Versions.targetSdk
        versionCode Versions.versionCode
        versionName Versions.versionName
        testApplicationId Packages.test
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "ja"
    }
    buildFeatures {
        dataBinding true
        viewBinding true
    }
    buildTypes {
        debug {
            testCoverageEnabled isCi
            minifyEnabled false
            applicationIdSuffix = '.debug'
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "default"
    productFlavors {
        prod {
            dimension "default"
        }
        demo {
            dimension "default"
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        abortOnError false
    }
    testOptions {
        animationsDisabled = true
        resultsDir = "$project.buildDir/test-results"
        unitTests.all {
            jvmArgs '-XX:MaxMetaspaceSize=256m'
            systemProperty 'debug', 'true'

            jacoco {
                includeNoLocationClasses true
            }
        }
        unitTests {
            includeAndroidResources true
            returnDefaultValues = true
        }
    }
    sourceSets {
        all {
            java.srcDirs += "src/${name}/kotlin"
        }
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation Dep.Kotlin.stdlibJvm
    implementation Dep.Kotlin.coroutines
    implementation Dep.Kotlin.coroutinesAndroid

    implementation Dep.Koin.core
    implementation Dep.Koin.android

    implementation Dep.AndroidX.appCompat
    implementation Dep.AndroidX.preference
    implementation Dep.AndroidX.card
    implementation Dep.AndroidX.coreKtx
    implementation Dep.AndroidX.activityKtx
    implementation Dep.AndroidX.fragmentKtx
    implementation Dep.AndroidX.design
    implementation Dep.AndroidX.constraintLayout

    implementation Dep.Zxing.core
    implementation(Dep.Zxing.android) {
        transitive = false
    }

    implementation Dep.OkHttp.client
    implementation Dep.Retrofit.client
    implementation Dep.Retrofit.converterGson

    implementation Dep.logger
    implementation Dep.eventBus

    testImplementation Dep.Test.junit
    testImplementation Dep.Koin.test
    testImplementation Dep.Test.archCore
    testImplementation Dep.Test.testCoreKtx
    testImplementation Dep.Test.androidJunit4Ktx
    testImplementation Dep.Test.testRunner
    testImplementation Dep.Test.testRules
    testImplementation Dep.Test.coroutinesTest
    testImplementation Dep.Test.Mockito.core
    testImplementation Dep.Test.Mockito.kotlin
    testImplementation Dep.Test.Robolectric.client
    testImplementation Dep.OkHttp.mockWebServer

    androidTestImplementation Dep.Test.androidJunit4Ktx
    androidTestImplementation Dep.Test.testRunner
    androidTestImplementation(Dep.Test.Espresso.core) {
        exclude module: 'support-annotations'
    }
    androidTestImplementation(Dep.Test.Espresso.contrib) {
        exclude module: 'support-annotations'
        exclude module: 'design'
        exclude module: 'recyclerview-v7'
        exclude module: 'support-v4'
    }
    androidTestImplementation Dep.OkHttp.mockWebServer
    androidTestImplementation Dep.Test.Mockito.android
}
